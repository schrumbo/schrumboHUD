plugins {
	id 'fabric-loom' version "${loom_version}" apply false
	id 'net.fabricmc.fabric-loom' version "${loom_version}" apply false
	id 'maven-publish'
	id 'com.modrinth.minotaur' version "2.+"
}

if (project.hasProperty('yarn_mappings')) {
	apply plugin: 'fabric-loom'
} else {
	apply plugin: 'net.fabricmc.fabric-loom'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenLocal()
	maven{
		name = "Terraformers"
		url = "https://maven.terraformersmc.com/"
	}
	maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
}


loom {
	splitEnvironmentSourceSets()
	accessWidenerPath = file("src/main/resources/schrumbohud.accesswidener")

	mods {
		"schrumbohud" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	if (project.hasProperty('yarn_mappings')) {
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
		modImplementation "schrumbo.schlib:schlib:1.0.2+${project.minecraft_version}"
		include "schrumbo.schlib:schlib:1.0.2+${project.minecraft_version}"
		if (project.hasProperty('modmenu_version') && project.modmenu_version != '') {
			modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
		}
	} else {
		implementation "net.fabricmc:fabric-loader:${project.loader_version}"
		implementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
		implementation "schrumbo.schlib:schlib:1.0.2+${project.minecraft_version}"
		include "schrumbo.schlib:schlib:1.0.2+${project.minecraft_version}"
		if (project.hasProperty('modmenu_version') && project.modmenu_version != '') {
			implementation("com.terraformersmc:modmenu:${project.modmenu_version}")
		}
	}
	if(project.hasProperty('devauth_version') && project.hasProperty('yarn_mappings')){
		modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${devauth_version}")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

def javaVersion = project.hasProperty('yarn_mappings') ? 21 : 25
def javaLangVersion = JavaVersion.toVersion(javaVersion)

tasks.withType(JavaCompile).configureEach {
	it.options.release = javaVersion
}

java {
	withSourcesJar()
	sourceCompatibility = javaLangVersion
	targetCompatibility = javaLangVersion
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "u4K3P3A5"
	versionNumber = "1.0.5"
	versionType = "release"
	uploadFile = project.hasProperty('yarn_mappings') ? remapJar : jar
	gameVersions = ["1.21.5", "1.21.6", "1.21.7", "1.21.8", "1.21.10", "1.21.10", "1.21.11"]
	loaders = ["fabric"]
	syncBodyFrom = rootProject.file("CHANGELOG.md").text
	dependencies {
		required.project "fabric-api"
	}
}
javadoc {
	source = sourceSets.main.allJava
	source += sourceSets.client.allJava

	classpath = configurations.compileClasspath
	classpath += sourceSets.client.compileClasspath

	options.encoding = 'UTF-8'
	options.charSet = 'UTF-8'
	options.memberLevel = JavadocMemberLevel.PRIVATE

	options.addStringOption('Xdoclint:none', '-quiet')

	destinationDir = file("${buildDir}/docs/javadoc")
}

task generateAllDocs(type: Javadoc) {
	source = sourceSets.main.allJava
	source += sourceSets.client.allJava

	classpath = configurations.compileClasspath
	classpath += sourceSets.client.compileClasspath

	options.encoding = 'UTF-8'
	options.memberLevel = JavadocMemberLevel.PRIVATE
	options.addStringOption('Xdoclint:none', '-quiet')

	destinationDir = file("${buildDir}/docs/all-javadoc")

	doFirst {
		println "Generating docs for:"
		source.files.each { println "  - ${it.name}" }
	}
}
task copyJavadocToGithubPages(type: Copy) {
	dependsOn javadoc
	from javadoc.destinationDir
	into file("${rootProject.projectDir}/docs")
}
